#!nsh
#
# Flight startup script for PX4FMU on PX4IOAR carrier board.
#
 
# Disable the USB interface
set USB no
 
# Disable autostarting other apps
set MODE ardrone
 
echo "[init] doing PX4IOAR startup..."
 
#
# Start the ORB
#
uorb start
 
#
# Init the parameter storage
#
echo "[init] loading microSD params"
param select /fs/microsd/parameters
if [ -f /fs/microsd/parameters ]
then
	param load /fs/microsd/parameters
fi
 
#
# Force some key parameters to sane values
# MAV_TYPE     1 = fixed wing, 2 = quadrotor, 13 = hexarotor
#              see https://pixhawk.ethz.ch/mavlink/
#
param set MAV_TYPE 2
 
#
# Start the sensors.
#
sh /etc/init.d/rc.sensors
 
#
# Start MAVLink
#
mavlink start -d /dev/ttyS0 -b 57600
usleep 5000
 
#
# Start the commander.
#
commander start
 
#
# Start the attitude estimator
#
attitude_estimator_ekf start
 
#
# Configure PX4FMU for operation with PX4IOAR
#
fmu mode_gpio_serial
 
#
# Fire up the multi rotor attitude controller
#
multirotor_att_control start
 
#
# Fire up the AR.Drone interface.
#
ardrone_interface start -d /dev/ttyS1
 
#
# Start GPS capture
#
gps start

#
# Start logging
#
sdlog start -s 10

#
# Start system state
#
if blinkm start
then
	echo "using BlinkM for state indication"
	blinkm systemstate
else
	echo "no BlinkM found, OK."
fi
 
#
# startup is done; we don't want the shell because we
# use the same UART for telemetry
#
echo "[init] startup done"
exit